apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.huawei.agconnect'

def static getGitVersion() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable th) {
        th.printStackTrace()
        return ""
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.appTargetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
        buildConfigField "String", "BUILD_DATE", "\"${new Date().toLocaleString()}\""
        multiDexEnabled true

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    signingConfigs {
        debug { storeFile file("debug.keystore") }

        release {
            storeFile file('release.keystore')
            storePassword 'thisiskeystorepassword'
            keyAlias 'nim_demo'
            keyPassword 'thisiskeypassword'
        }
    }

    buildTypes {
        debug {
//            debuggable false
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile("proguard-android.txt"), 'proguard.cfg'
            signingConfig signingConfigs.debug
            manifestPlaceholders = [AMAP_KEY: "09fd4efd3e28e9bf1f449ecec7d34bfe"]
            ext.enableCrashlytics = false
        }

        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), 'proguard.cfg'
            signingConfig signingConfigs.release
            manifestPlaceholders = [AMAP_KEY: "ee20324fba1c7f4ad7a4a207e7f08e8d"]
            ext.enableCrashlytics = true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res', 'res-chatroom', 'res-rp']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        common.java.srcDir('common')
        sf.java.srcDir('sf')
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries false
        jumboMode true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
    }

    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }

    flavorDimensions "rc"
    productFlavors {
        common {
            dimension "rc"
        }

        sf {
            dimension "rc"
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.appSourceCompatibility
        targetCompatibility rootProject.ext.appTargetCompatibility
    }

    dataBinding {
        enabled = true
    }
}

apply plugin: 'com.google.gms.google-services'

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation project(path: ':uikit')
    implementation project(':jrmfRpWalletlib')
    implementation project(path: ':rtskit')

    implementation project(path: ':G2:ui')
    implementation project(path: ':G2:nertcvideocall')

    //debugImplementation project(':plugins:sf')

    implementation 'com.huawei.hms:push:4.0.4.301'
    implementation 'com.meizu.flyme.internet:push-internal:3.9.7'

    implementation 'com.netease.nimlib:jsbridge:1.3.1'

    implementation 'com.google.android.gms:play-services-base:17.1.0'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.firebase:firebase-core:17.2.3'
    //implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    // Java8 support for Lifecycles
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.1.0'


    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
}
